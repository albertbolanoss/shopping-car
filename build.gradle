plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.perficient.shoppingcart'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

ext {
    springVersion = '3.2.4'
}

processResources {
    dependsOn(generateSwaggerCode)
}

swaggerSources {
    def typeMappings = 'URI=URI'
    def importMappings = 'URI=java.net.URI'
    shoppingCar {
        def apiYaml = "${rootDir}/src/main/resources/api/openapi.yaml"
        def configJson = "${rootDir}/src/main/resources/api/config.json"
        inputFile = file(apiYaml)
        def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
        code {
            language = 'spring'
            configFile = file(configJson)
            rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
                          typeMappings, '--import-mappings', importMappings] as List<String>
            components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
            dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.shoppingCar.code
sourceSets.main.java.srcDir "${swaggerSources.shoppingCar.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.shoppingCar.code.outputDir}/src/main/resources"


repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    developmentOnly "org.springframework.boot:spring-boot-devtools:$springVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-cache:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-redis:$springVersion"
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'com.auth0:java-jwt:4.3.0'

    // OpenAPI
    swaggerCodegen 'org.openapitools:openapi-generator-cli:6.2.1'
    compileOnly 'io.swagger:swagger-annotations:1.6.4'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'io.springfox:springfox-oas:3.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.4.0'
    implementation 'org.springdoc:springdoc-openapi-data-rest:1.8.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    // Mysql Driver
    implementation 'com.mysql:mysql-connector-j:8.3.0'

    // Liquibase Migration
    implementation 'org.liquibase:liquibase-core:4.26.0'

    implementation 'net.datafaker:datafaker:2.1.0'

    // H2 Driver
    implementation  'com.h2database:h2:2.2.224'

}

tasks.named('test') {
    useJUnitPlatform()
}